apply plugin: "jacoco"

def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
def classes = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
def sources = files("$projectDir/src/main/kotlin")

jacoco.toolVersion "0.8.6"

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoCoverageReport(type: JacocoReport) {
    sourceDirectories.setFrom(sources)
    classDirectories.setFrom(files(classes))
    executionData.setFrom(fileTree(dir: buildDir, includes: [
            "jacoco/*.exec",
            "outputs/code_coverage/debugAndroidTest/connected/**/*.ec"
    ]))

    reports {
        xml.enabled = true
        html.enabled = true
    }

    doLast {
        println "JaCoCo report has been generated to file://${reports.html.destination}/index.html"
    }
}
